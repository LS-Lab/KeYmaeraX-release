Functions.
  R g.      /* gravity */
  R ep.     /* time limit for control decisions */
  R B.      /* minimum braking capability of the aircraft */
  R A.      /* maximum acceleration -B <= a <= A */
  R W.      /* maximum roll rate of -W <= a <= W */
  R Theta.  /* maximum roll angle */
  R Vmin.   /* minimum cruising speed */
  R abs(R). /* predefined function of absolute value */
  R D.      /* radar range */
  R S.      /* known safe zone width // S = D * sin(view_angle) */
  R tanTheta. /* value of tan(Theta) */
End.

ProgramVariables.
  R xo.     /* obstacle position: xo */
  R yo.     /* obstacle position: yo */
  R x.      /* aircraft position: x */
  R y.      /* aircraft position: y */
  R v.      /* aircraft velocity in x-y plane */  R t.      /* time */

  R a.      /* aircraft acceleration in x-y plane */
  R dx.     /* aircraft orientation: x component of heading psi // dx = cos(psi) */
  R dy.     /* aircraft orientation: y component of heading psi // dy = sin(psi) */
  R dz.     /* aircraft orientation: z component of roll angle theta // dz = cos(theta) */
  R dxy.    /* aircraft orientation: x-y plane component of roll angle theta // dxy = sin(theta) */
  R theta.  /* roll angle */
  R w.      /* roll rate */
  R sqrdr.  /* square root of (d^2+r^2) */
/*
  R dv.     travel distance when v: v -> Vmin // dv = (v^2-Vmin^2)/(2*B)
  R dt.     travel distance when v: v -> Vmin and stay Vmin until theta: theta -> Theta // dt = (v-Vmin)^2/(2*B)+Vmin*(Theta-abs(theta))/W
  R dep.    travel distance in ep time // dep = (A/B+1)*(A/2*ep^2+ep*v)
  R r.      radius for the loiting circle // r = (Vmin^2)/(g*tanTheta)
  R tv.     time for v: v -> Vmin // tv = (v-Vmin)/B
  R tt.     time for theta: theta -> Theta // tt = (Theta-abs(theta))/W
*/
End.

Problem.
  dx^2+dy^2 = 1
  & dz^2+dxy^2 = 1
  & g > 0
  & A >= 0
  & B > 0
  & W > 0
  & Theta > 0
  & theta <= Theta
  & theta >= -Theta
  & tanTheta > 0
  & Vmin > 0
  & v >= Vmin
  & sqrdr > 0
  & ep > 0
  & (((v-Vmin)/B >= (Theta-abs(theta))/W &
    (sqrdr^2 = ((v^2-Vmin^2)/(2*B))^2 + ((Vmin^2)/(g*tanTheta))^2
    & S >= sqrdr + (Vmin^2)/(g*tanTheta)
    & (abs(x-xo) >= (v^2-Vmin^2)/(2*B) + 2*(Vmin^2)/(g*tanTheta)
      | abs(y-yo) >= (v^2-Vmin^2)/(2*B) + 2*(Vmin^2)/(g*tanTheta))
    ))
  | ((v-Vmin)/B < (Theta-abs(theta))/W &
    (sqrdr^2 = ((v-Vmin)^2/(2*B)+Vmin*(Theta-abs(theta))/W)^2 + ((Vmin^2)/(g*tanTheta))^2
    & S >= sqrdr + (Vmin^2)/(g*tanTheta)
    & (abs(x-xo) >= (v-Vmin)^2/(2*B)+Vmin*(Theta-abs(theta))/W + 2*(Vmin^2)/(g*tanTheta)
      | abs(y-yo) >= (v-Vmin)^2/(2*B)+Vmin*(Theta-abs(theta))/W + 2*(Vmin^2)/(g*tanTheta))
    )))
  ->
  [
  {
    {
      {
        /* make a circle */
        {
          /* control the acceleration */
          {{ ?v = Vmin; a := 0; }
          ++
          { ?v >= Vmin; a := -B; }};

          /* control the roll angle */
          /* follow a circle */
          {{ ?abs(theta) = Theta; w :=0; }
          ++
          /* or turn right to circle */
          { ?0 <= theta & theta < Theta; w := W; }
          ++
          /* or turn left to circle */
          { ?-Theta < theta & theta < 0; w := -W; }}
        }
        ++
        /* or choose a new safe curve */
        { a :=*; ?-B <= a & a <= A;
          w :=*; ?-W <= w & w <= W;
          v :=*; ?v>=Vmin;
          theta :=*; ?-Theta <= theta & theta <= Theta;
          /* for the chosen a, w, v, theta: worst case position of obstacles wrt. curve  */
          xo :=*;
          yo :=*;
          /* use that curve, if it is a safe one (admissible velocities) */
          ? (((v-Vmin)/B >= (Theta-abs(theta))/W ->
                 (sqrdr^2 = ((v^2-Vmin^2)/(2*B) + (A/B+1)*(A/2*ep^2+ep*v))^2 + ((Vmin^2)/(g*tanTheta))^2
                 & S >= sqrdr + (Vmin^2)/(g*tanTheta)
                 & (abs(x-xo) >= (v^2-Vmin^2)/(2*B) + (A/B+1)*(A/2*ep^2+ep*v) + 2*(Vmin^2)/(g*tanTheta)
                   | abs(y-yo) >= (v^2-Vmin^2)/(2*B) + (A/B+1)*(A/2*ep^2+ep*v) + 2*(Vmin^2)/(g*tanTheta))
                 ))
               & ((v-Vmin)/B < (Theta-abs(theta))/W ->
                 (sqrdr^2 = ((v-Vmin)^2/(2*B)+Vmin*(Theta-abs(theta))/W + (A/B+1)*(A/2*ep^2+ep*v))^2 + ((Vmin^2)/(g*tanTheta))^2
                 & S >= sqrdr + (Vmin^2)/(g*tanTheta)
                 & (abs(x-xo) >= (v-Vmin)^2/(2*B)+Vmin*(Theta-abs(theta))/W + (A/B+1)*(A/2*ep^2+ep*v) + 2*(Vmin^2)/(g*tanTheta)
                   | abs(y-yo) >= (v-Vmin)^2/(2*B)+Vmin*(Theta-abs(theta))/W + (A/B+1)*(A/2*ep^2+ep*v) + 2*(Vmin^2)/(g*tanTheta))
                 )));
        }
      };
      /* reset time */
      t := 0;
    }
    /* dynamics */
    { x' = v * dx,
      y' = v * dy,
      dx' = -dy * (g/v) * (dxy/dz),
      dy' = dx * (g/v) * (dxy/dz),
      dz' = -dxy * w,
      dxy' = dz * w,
      v' = a,
      theta' = w,
      t' = 1 & t <= ep & v >= Vmin & -Theta <= theta & theta <= Theta
    }
    /*@invariant(
      t>=0,
      dx^2+dy^2 = 1,
      dz^2+dxy^2 = 1,
      v = v0 + a*t,
      theta = theta0 + w*t
    )*/
  }* /*@invariant(
      dx^2+dy^2 = 1
      & dz^2+dxy^2 = 1
      & v>=Vmin
      & theta <= Theta
      & theta >= -Theta
      & (((v-Vmin)/B >= (Theta-abs(theta))/W &
        (sqrdr^2 = ((v^2-Vmin^2)/(2*B))^2 + ((Vmin^2)/(g*tanTheta))^2
        & S >= sqrdr + (Vmin^2)/(g*tanTheta)
        & (abs(x-xo) >= (v^2-Vmin^2)/(2*B) + 2*(Vmin^2)/(g*tanTheta)
          | abs(y-yo) >= (v^2-Vmin^2)/(2*B) + 2*(Vmin^2)/(g*tanTheta))
        ))
      | ((v-Vmin)/B < (Theta-abs(theta))/W &
        (sqrdr^2 = ((v-Vmin)^2/(2*B)+Vmin*(Theta-abs(theta))/W)^2 + ((Vmin^2)/(g*tanTheta))^2
        & S >= sqrdr + (Vmin^2)/(g*tanTheta)
        & (abs(x-xo) >= (v-Vmin)^2/(2*B)+Vmin*(Theta-abs(theta))/W + 2*(Vmin^2)/(g*tanTheta)
          | abs(y-yo) >= (v-Vmin)^2/(2*B)+Vmin*(Theta-abs(theta))/W + 2*(Vmin^2)/(g*tanTheta))
        )))
    )*/
  ] (x - xo)^2 + (y - yo)^2 > 0
End.
