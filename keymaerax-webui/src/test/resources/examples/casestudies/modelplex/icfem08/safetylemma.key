/**
 * Safety lemma for European Train Control System
 * @provable
 * The following refinement is provable, too: SB := (v^2 - d^2)/(2*b) + (a/b+1)*(A/2*ep^2+ep*v);
 * @see "Andre Platzer and Jan-David Quesel. European Train Control System: A case study in formal verification. In Karin Breitman and Ana Cavalcanti, editors, 11th International Conference on Formal Engineering Methods, ICFEM, Rio de Janeiro, Brasil, Proceedings, volume 5885 of LNCS, pages 246-265. Springer, 2009."
 */
Functions.
  R b.
  R A.
  R ep.
End.

ProgramVariables.
	R vdes.
	R SB.
	R mo.
	R t.
	R a.
	R v.
	R z.
	R m.
	R d.
	R do.
	R drive.
	R brake.
	R state.
End.

Problem.
    drive = 0
  & brake = 1
  & state = drive
  & v^2 - d^2 <= 2*b*(m-z)
  & d >=0
  & b > 0
  & A >= 0
  & ep >= 0
->
  [
  {
       {
           {do := d; mo := m; m :=*; d :=*; vdes :=*; ?d >= 0 & do^2 - d^2 <= 2*b*(m-mo) & vdes >= 0;}
        ++ {state := brake;}
       }
    ++ {
        {  {?v <= vdes; a :=*; ?a >=-b & a <= A;}
        ++ {?v >= vdes; a :=*; ?a <0 & a >= -b;}}
        SB := (v^2 - d^2)/(2*b) + (A/b+1)*(A/2*ep^2+ep*v);
        {  {?  m - z <= SB | state = brake; a := -b;}
        ++ {?!(m - z <= SB | state = brake);}}
        t := 0; {z'=v, v' = a, t'=1 & v >= 0 & t <= ep}
       }
  }*@invariant(v^2 - d^2 <= 2*b*(m-z) & d >= 0)
  ](z >= m -> v <= d)
End.
