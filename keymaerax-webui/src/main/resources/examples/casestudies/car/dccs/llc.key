Functions.
  R ep.
  R b.
  R B.
  R A.
End.

ProgramVariables.
  R xl.
  R vl.
  R al.
  R xf.
  R vf.
  R af.
  R t.
End.

Problem.
    xf <  xl
  & xl > xf + vf^2/(2*b) - vl^2/(2*B)
  & B >= b
  & b >  0
  & vf >= 0
  & vl >= 0
  & A >= 0
  & ep >  0
-> [
     { al :=*; ?-B <= al & al <= A;
       {
          ?xf + vf^2/(2*b) + (A/b + 1)*(A/2 * ep^2 + ep*vf) < xl + vl^2/(2*B);
          af :=*;
          ?-B <= af & af <= A;
       ++ ?vf = 0; af := 0;
       ++ af :=*; ?-B <= af & af <= -b;
       };
       t := 0;
       xf' = vf, vf' = af, xl' = vl, vl' = al, t' = 1 & vf >= 0 & vl >= 0 & t <= ep
     }* /*@invariant(vf>=0 & vl>=0 & xf<xl & xl > xf + vf^2/(2*b) - vl^2/(2*B))*/
   ] xf <  xl
End.