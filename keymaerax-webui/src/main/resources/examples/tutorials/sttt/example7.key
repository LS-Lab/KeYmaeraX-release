Functions.
  R A.
  R B.
  R b.
  R S.
  R ep.
  R a().
End.

ProgramVariables.
  R x.
  R v.
  R a.
  R c.
End.

Problem.
    v >= 0 & A > 0 & B >= b & b > 0 & x+v^2/(2*b) <= S & ep > 0
 -> [
      { {   ?x+v^2/(2*b) + (A/b+1)*(A/2*ep^2+ep*v) <= S; a := *; ?-B <= a & a <= A
         ++ ?v=0; a := 0
         ++ a := *; ?-B <=a & a <= -b
        };

        ?a()=a; /* needed because we neither support a() := nor v'=a */
        c := 0;
        x' = v, v' = a(), c' = 1 & v >= 0 & c <= ep;
      }*@invariant(v >= 0 & x+v^2/(2*b) <= S)
    ] x <= S
End.