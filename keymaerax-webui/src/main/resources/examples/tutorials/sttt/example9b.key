Functions.
  R Kp.
  R Kd.
  R S.
End.

ProgramVariables.
  R x.
  R xm.
  R xr.
  R v.
End.

Problem.
    v >= 0 & xm <= x & x <= S & xr = (xm + S)/2 & Kp = 2 & Kd = 3
           & 5/4*(x-xr)^2 + (x-xr)*v/2 + v^2/4 < ((S - xm)/2)^2
 -> [ { {  xm := x;
           xr := (xm + S)/2;
           ?5/4*(x-xr)^2 + (x-xr)*v/2 + v^2/4 < ((S - xm)/2)^2;
        ++ xm := xm; xr := xr; /* noop assignments instead ?true */
        };
        { x' = v, v' = -Kp*(x-xr) - Kd*v & v >= 0 }
      }* /*@invariant(v >= 0 & xm <= x & xr = (xm + S)/2 & 5/4*(x-xr)^2 + (x-xr)*v/2 + v^2/4 < ((S - xm)/2)^2)*/
    ] x <= S
End.