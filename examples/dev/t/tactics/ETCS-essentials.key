/**
 * Essentials of European Train Control System (ETCS)
 * @see "Andre Platzer. Differential dynamic logic for hybrid systems. Journal of Automated Reasoning, 41(2), pages 143-189, 2008."
 * @provable
 */
Functions.
  R ep.
  R b.
  R A.
End.

ProgramVariables.
  R SB.
  R a.
  R v.
  R z.
  R t.
  R v_0.
  R z_0.
  R m.
End.

Problem.
    ( v^2 <= 2*b*(m-z) & b > 0 & A>=0)
  -> 
   [{
    SB := (v^2)/(2*b) + ((A/b) + 1)*((A/2)*ep^2 + ep*v);
    { {?m - z <= SB; a:= -b} ++ {?m - z >= SB; a:=A}};
    {z_0:=z; v_0:=v; z:=*; v:=*; t:=*; ?z=1/2*a*t^2 + v_0*t + z_0 & v=a*t+v_0 & v >= 0 & t <= ep}
    }*] (z <= m)
End.
