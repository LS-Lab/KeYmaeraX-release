Functions.
  R ep.
  R B.
  R A.
  R V.
  external R Abs(R).
End.

ProgramVariables.
  R x.
  R y.
  R v.
  R a.
  R dx.
  R dy.
  R w.
  R r.
  R xo.
  R yo.
  R dxo.
  R dyo.
  R t.
End.

Problem.
    v >= 0
  & (Abs(x - xo) > v^2 / (2*B) + V*v/B | Abs(y - yo) > v^2 / (2*B) + V*v/B)
  & r != 0
  & dx^2 + dy^2 = 1
  & A >= 0
  & B > 0
  & V >= 0
  & ep > 0
          -> [{	/* control obstacle */
          		  dxo := *;
          			dyo := *;
          			?dxo^2 + dyo^2 <= V^2;

          			/* brake on current curve or remain stopped */
          			{
          			{a := -B;}
          		++{?v = 0; a := 0; w := 0;}
          			/* or choose a new safe curve */
    				  ++{a := *; ?-B <= a & a <= A;

    					   r := *; ?r != 0; /* do not spin */
    					   w := *; ?w * r = v;

                 /* for the chosen a, w, cx, cy: worst case position of obstacles wrt. curve */
                 xo := *;
                 yo := *;

    					   /* use that curve, if it is a safe one (admissible velocities) */
    					   ? Abs(x - xo) > v^2/(2*B) + V*v/B + (A/B + 1) * (A/2 * ep^2 + ep*(v+V))
    						 | Abs(y - yo) > v^2/(2*B) + V*v/B + (A/B + 1) * (A/2 * ep^2 + ep*(v+V));
    				    };
    				    };

    				    t := 0;

    				    /* dynamics */
    				    x' = v * dx, y' = v * dy, dx' = -w * dy, dy' = w * dx, v' = a, w' = a/r,
    						    xo' = dxo, yo' = dyo,
    						    t' = 1, t <= ep, v >= 0;

                /*@invariant(
                    t >= 0,
                    dx^2 + dy^2 = 1
                    v = v0 + a*t,
                    -t * (v - a/2*t) <= x - x0 & x - x0 <= t * (v - a/2*t),
                    -t * (v - a/2*t) <= y - y0 & y - y0 <= t * (v - a/2*t),
                    -t * V <= xo - xo0 & xo - xo0 <= t * V,
                    -t * V <= yo - yo0 & yo - yo0 <= t * V
                  )*/
    			    }*@invariant(v >= 0
                  & dx^2+dy^2 = 1
                  & r != 0
                  & (v = 0 | Abs(x - xo) > v^2 / (2*B) + V * v/B | Abs(y - yo) > v^2 / (2*B) + V * v/B)
                )
    		] (v != 0 -> (x - xo)^2 + (y - yo)^2 > 0)
End.