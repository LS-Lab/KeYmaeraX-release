/*
 * KeYmaera Axioms
 * @author Nathan Fulton
 * @author Jan-David Quesel
 * @author Andre Platzer
 * 
 * Basic dL Axioms of Differential Dynamic Logic.
 * @see "Andre Platzer. The complete proof theory of hybrid systems. ACM/IEEE Symposium on Logic in Computer Science, LICS 2012, June 25â€“28, 2012, Dubrovnik, Croatia, pages 541-550. IEEE 2012."
 * @see "Andre Platzer. Dynamic logics of dynamical systems. arXiv 1205.4788, May 2012."
 */
Variables.
  T s.
  T t.
  T x.
  T v. /* TODO-nrf this needs to be a V */
  T f(T).
  T g(T).
  P a.
  P b.
  CP c.
  F p.
  F p(T).
  F q.
  F H.
  F H(T).
End.

Axiom "[:=] assignment".
  [v:=t;]p(v) <-> p(t)
End.

Axiom "<:=> assignment".
  <v:=t;>p(v) <-> p(t)
End.

Axiom "[:=] assignment equal".
  [v:=t;]p(v) <-> \forall v . (v=t -> p(v))
End.

Axiom "[':=] derivative assignment equal".
  [v':=t;]p(v) <-> \forall v . (v'=t -> p(v))
End.

Axiom "[:*] assignment".
  [v:=*;]p(v) <-> \forall v. p(v)
End.

Axiom "<:*> assignment".
  <v:=*;>p(v) <-> \exists v. p(v)
End.

Axiom "[?] test".
  [?H;]p <-> (H -> p).
End.

Axiom "<?> test".
  <?H;>p <-> (H & p).
End.

Axiom "[++] choice".
  [a ++ b]p <-> ([a;]p & [b;]p).
End.

/*
Axiom "<++> choice".
   <a ++ b>p <-> (<a;>p | <b;>p).
End.
*/

Axiom "[;] compose".
  [ a; b; ]p <-> [a;][b;]p.
End.

Axiom "<;> compose".
  < a; b; >p <-> <a;><b;>p.
End.

Axiom "[*] unwind".
  [a*]p <-> (p & [a;][a*] p).
End.

Axiom "<*> unwind".
  <a*>p <-> (p | <a;><a*> p).
End.

Axiom "K modal modus ponens".
  [a;](p->q) -> (([a;]p) -> ([a;]q))
End.

Axiom "I induction".
  (p & [a*](p -> [a;] p)) -> [a*]p
End.

Axiom "B- converse Barcan".
  ([a;] \forall x. p(x)) -> \forall x. [a;] p(x)
End.

Axiom "Quantifier Instantiation".
  \forall x. p(x) -> p(t)
End.

/* @TODO ap: corrected parentheses
// \forall {a} quantifies over all variables written in a
// not sure if this can be an axiom or needs to be a built-in rule
// since it's hard to get this working with uniform substitution
Axiom "QuantifierAbstraction".
  (\forall {a}. p) -> ([a] p)
End.
Axiom "QuantifierAbstraction".
  \forall x . p(x) -> [a(x)]p(x)
End.
*/

/*
Axiom "Forall-Quantifier Scope".
  (\forall x. (p(x) & q)) <-> ((\forall x. p(x)) & q)
End.
*/

/* @TODO does this parse?
   @TODO How to deal with unchanged variables and systems?
   @TODO check
*/
Axiom "DI differential invariant".
  [x'=t&H;]p <- ([x'=t&H;](H->[x':=t;](p')))
End.

/* @TODO why are these equivalent? */
Axiom "DI Differential invariant".
  [x'=t&H;]p <- ( (H->p) & ([x':=t;]p') )
End.

Axiom "DC differential cut".
    [x'=t&H;]p <- ([x'=t&H;]q & [x'=t&H&q;]p)
End.

/* diff. weaken of differential equation system */
Axiom "DW differential weaken system introduce".
  [c;]p <- [$$c$$;]p
End.
Axiom "DW differential weaken system head".
  [$$x'=f(x)&H(x),c$$;]p(x) <- [x:=*;][$$c$$;][?H(x);]p(x)
End.
Axiom "DW differential weaken system nil".
  [$$$$;][?H;]p <- [?H;]p
End.

/* @TODO this is identical to the DW differential weaken system introduce rule. */
Axiom "DI System Introduction".
  [c;]p <- [ $$c$$; ]p
End.

Axiom "DI System Head Elimination (test)".
  [ $$x' = f(x) & H(x), c$$; ][?H;]p <- [x' := f(x);] [$$c$$;][?H & H(x);]p
End.

Axiom "DI System Head Elimination (no test)".
  [ $$x' = f(x) & H(x), c$$; ]p <- [x' := f(x);] [$$c$$;][?H(x);]p
End.

/* @TODO talk with Andre about 1) when we should prime; 2) a minimal set of axioms for '; 3) an implementation of the prime tactics */
Axiom "DI System Nil Elimination".
  [$$$$;][?H;]p <- [?H;]p'
End.

/* syntactic total derivation of formulas */

Axiom "&' derive and".
  (p & q)' <-> ((p') & (q'))
End.

Axiom "|' derive or".
  (p | q)' <-> ((p') & (q'))
End.

Axiom "=' derive =".
  (s = t)' <-> ((s') = (t'))
End.

Axiom ">=' derive >=".
  (s >= t)' <-> ((s') >= (t'))
End.

Axiom ">' derive >".
  (s > t)' <-> ((s') >= (t'))
End.

Axiom "<=' derive <=".
  (s <= t)' <-> ((s') <= (t'))
End.

Axiom "<' derive <".
  (s < t)' <-> ((s') <= (t'))
End.

Axiom "!=' derive !=".
  (s != t)' <-> ((s') = (t'))
End.

/* syntactic total derivation of terms */

Axiom "-' derive neg".
  (-s)' = -(s')
End.

Axiom "+' derive sum".
  (s + t)' = (s') + (t')
End.

Axiom "-' derive minus".
  (s - t)' = (s') - (t')
End.

Axiom "*' derive product".
  (s * t)' = ((s')*t) + (s*(t'))
End.

Axiom "/' derive quotient".
  (s / t)' = (((s')*t) - (s*(t'))) / (t^2)
End.

/*
Axiom "chain rule".
	p((f(g(x)))') <-> \forall u. (u = g(x) -> p((g(x))'*(f(u))'))
End.
*/

/* @TODO
Axiom "Derive Monomial".
	(x^n)' = n*x^(n-1)
End.
*/
